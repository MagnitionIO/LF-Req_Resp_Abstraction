target C {
    keepalive: true,
    cmake-include: [
        "../lib/lib.cmake"
    ],
    files: [
        "../lib/convertor.h",
        "../lib/convertor.c",
        "../lib/types.h"
    ]
    // fast: true
}

preamble {=
#include <stdio.h>
#include "convertor.h"
=}

reactor Wire<Input, Output> (latency:int(0)){
    input in:Input;
    output out:Output;
    logical action delayed_trigger(0):Input;

    reaction(in) -> delayed_trigger {=
        lf_schedule_copy(delayed_trigger, self->latency, &in->value, 1);
    =}

    reaction(delayed_trigger) -> out {=
        if (is_same_type(delayed_trigger->value, out->value))
        {
            lf_set(out, delayed_trigger->value);
        }
        else
        {
            auto_t tmp = DO_CONVERT(Input, Output, delayed_trigger->value);
            lf_set(out, tmp);
        }
    =}
}

reactor A {
    output Aout:int;
    logical action post;

    timer t(0, 1sec);
    reaction(t) -> post {=
        lf_schedule(post, 2);
    =}

    reaction(post) -> Aout {=
        lf_set(Aout, rand());
    =}
}

reactor B {
    input Bin:float;

    reaction(Bin) {=
        printf("got %f\n", Bin->value);
    =}
}

main reactor {
    a = new A();
    w = new Wire<int, float>(latency=500);
    w2 = new Wire<long, double>(latency=500);
    b = new B();

    a.Aout -> w.in;
    w.out -> b.Bin;
}
