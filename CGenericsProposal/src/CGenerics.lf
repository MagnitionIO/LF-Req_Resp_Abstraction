target C {
    keepalive: true,
    cmake-include: [
        "../lib/lib.cmake"
    ],
    files: [
        "../lib/convertor.h",
        "../lib/convertor.c"
    ]
    // fast: true
}

preamble {=
#include <stdio.h>
#include "convertor.h"
=}

reactor Connector<I, O>(latency:int(0)) {
    input in:I;
    output out:O;
    logical action delayed_sch;

    reaction(in) -> delayed_sch {=
        lf_schedule(delayed_sch, self->latency);
    =}

    reaction(delayed_sch) in -> out {=
        if (is_same_type(in->value, out->value))
        {
            lf_set(out, in->value);
        }
        else
        {
            auto_t tmp = CALL_CONVERTOR(I, O, in->value);
            lf_set(out, tmp);
        }
    =}
}

reactor A {
    output Aout:int;
    logical action post;

    timer t(0, 1sec);
    reaction(t) -> post {=
        lf_schedule(post, 2);
    =}

    reaction(post) -> Aout {=
        lf_set(Aout, rand());
    =}
}

reactor B {
    input Bin:float;

    reaction(Bin) {=
        printf("got %f\n", Bin->value);
    =}
}

main reactor {
    a = new A();
    w = new Connector<int, float>(latency=500);
    b = new B();

    a.Aout -> w.in;
    w.out -> b.Bin;
}
